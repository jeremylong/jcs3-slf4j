plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.cyclonedx.bom' version '1.7.2'
    id 'com.diffplug.spotless' version '6.12.0'
}

group 'io.github.jeremylong'
version = '1.0.1'
description = 'slf4j binding for JCS3 logging'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'ch.qos.logback:logback-core:1.2.11'
    testImplementation 'ch.qos.logback:logback-classic:1.2.11'

    implementation 'org.apache.commons:commons-jcs3-core:3.0'
    implementation 'org.slf4j:slf4j-api:1.7.36'
}

tasks.named('test') {
    useJUnitPlatform()
}
compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
spotless {
    java {
        eclipse().configFile("$rootDir/buildSrc/src/main/config/spotless.eclipseformat.xml")
        licenseHeaderFile("$rootDir/buildSrc/src/main/config/java.license").updateYearWithLatest(true)
    }
}
spotlessJava.dependsOn 'compileJava'
spotlessJava.dependsOn 'compileTestJava'
build.finalizedBy 'spotlessApply'
publish.dependsOn 'cyclonedxBom'

javadoc {
    failOnError = false
    options.encoding("UTF-8")
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
java {
    withSourcesJar()
    withJavadocJar()
}
signing {
    sign publishing.publications
}
sourcesJar.dependsOn(compileJava)
sourcesJar.dependsOn(compileTestJava)
javadoc.dependsOn(compileTestJava)

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
            pom {
                url = 'https://github.com/jeremylong/jcs3-slf4j/'
                description = project.description
                name = project.name
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/jeremylong/jcs3-slf4j/blob/main/LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jeremy.long'
                        name = 'Jeremy Long'
                    }
                }
                scm {
                    url = 'https://github.com/jeremylong/jcs3-slf4j'
                    connection = 'scm:git:https://github.com/jeremylong/jcs3-slf4j.git'
                    developerConnection = 'scm:git:https://github.com/jeremylong/jcs3-slf4j.git'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username project.findProperty('sonatypeUsername') ?: System.getenv("SONATYPE_USER")
                password project.findProperty('sonatypePassword') ?: System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}